# vim:syntax=tcsh
#
# tcsh configuration
# Martin Tournoij <martin@arp242.net>
# Should work on: FreeBSD, OpenBSD, Linux (CentOS, Ubuntu, Arch), OpenSolaris, Windows 7
#

# NetBSD
if (-d /usr/pkg/bin) then
	set prefix = /usr/pkg
# OpenSolaris
else if (-d /opt/csw/bin) then
	set prefix = /opt/csw
# FreeBSD, OpenBSD
else if (-d /usr/local/bin) then
	set prefix = /usr/local
else
    set prefix = 0
endif

if (-X uname) then
	set uname = `uname`
	# uname part of msysgit
	if ($uname == MINGW32_NT-6.1) set uname = win32
else
	set uname = win32
endif

umask 022

source ~/.tcsh/env.tcsh
source ~/.tcsh/aliases.tcsh

# Only load these for interactive shells
if ($?prompt) then
	# Disable ^S/^Q - I never use them and I can use those keys in Vim now
	stty -ixon

	source ~/.tcsh/settings.tcsh
	source ~/.tcsh/keybinds.tcsh
	source ~/.tcsh/completions.tcsh

	# We're ssh-ing into vagrant
	if (-d /vagrant) then
		cd /vagrant
		setenv TERM xterm
		# Don't logout too easily
		bindkey -r ^D
	endif
endif
