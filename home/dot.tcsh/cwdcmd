# vim:set syntax=tcsh

echo -n "\033]2;tcsh: $cwd\007"

unset printexitvalue

# Only get the branch once, for the sake of efficiency
if ( $?reporoot ) then
	if ( "$cwd" =~ "$reporoot*" ) then
		goto end
	endif
endif

hg branch >& /dev/null
if ( $? == 0 ) then
	set repo = `hg branch`
	#set prompt = "%{\033[35m%}(hg:$repo)%{\033[0m%}[%~]%# "
	set prompt = "[%~]($repo)%# "
	set reporoot = `hg root`
	goto end
endif

git rev-parse --abbrev-ref HEAD >& /dev/null
if ( $? == 0 ) then
	set repo = `git rev-parse --abbrev-ref HEAD`
	#set prompt = "%{\033[35m%}(git:$repo)%{\033[0m%}[%~]%# "
	set prompt = "[%~]($repo)%# "
	set reporoot = `git rev-parse --show-toplevel`
	goto end
endif 

set prompt = "[%~]%# "
unset reporoot


end:
	set printexitvalue
